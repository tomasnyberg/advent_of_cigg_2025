let lines = filter (l => l != "") words "\n" read_file("inputs/day6.in");

fn to_nums(line) {
  return map int filter (l => l != "") map trim words " " line;
}

let lines_as_nums = map to_nums lines[0..len(lines) - 1];
let ops = filter (l => l != "") map trim words " " lines[len(lines) - 1];

let part_one = 0;
for i in 0..len(lines_as_nums[0]) {
  let nums = map (idx => lines_as_nums[idx][i]) [0..len(lines_as_nums)];
  part_one += ops[i] == '+' ? sum(nums) : prod(nums);
}
print("Part one: " + part_one);

let matrices = [];
let matrix = map (i => []) [0..len(lines)];
for c in 0..len(lines[0]) {
  if (filter (c => c != ' ') map (i => lines[i][c]) [0..len(lines)]) == [] {
    matrices :: matrix;
    matrix = map (i => []) [0..len(lines)];
    continue;
  }
  for i in 0..len(lines) {
    matrix[i] :: lines[i][c];
  }
}
matrices :: matrix;

fn calc_matrix(matrix) {
  let nums = [];
  for c in 0..len(matrix[0]) {
    let num = "";
    for i in 0..len(matrix) - 1 {
      if matrix[i][c] != ' ' {
        num :: matrix[i][c];
      }
    }
    nums :: int(num);
  }
  return matrix[len(matrix) - 1][0] == '+' ? sum(nums) : prod(nums);
}

print("Part two: " + sum(map calc_matrix matrices));

